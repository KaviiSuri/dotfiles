{{ if eq .chezmoi.os "windows" -}}
@REM -*-mode:bat-*- vim:ft=bat
@echo off

REM
REM Copy Hyper configuration file to final destination on Windows
REM
REM This file is parsed and run automatically after each `chezmoi apply`.
REM

set SRCFILE=%USERPROFILE%\.hyper.js
set DESTFILE=%APPDATA%\Hyper\.hyper.js

REM Application has not been installed
FOR %%A in (%DESTFILE%) do (
    IF NOT EXIST %%~pA (
        exit 0
    )
)

REM Destination is a symbolic link
dir %DESTFILE% | find "<SYMLINK>" > nul
IF %errorlevel% == 0 (
    exit 0
)

REM Configuration files are identical
fc /b %SRCFILE% %DESTFILE% > nul
IF %errorlevel% == 0 (
    exit 0
)

REM Files are different or destination does not exist
xcopy /fvy %SRCFILE% %DESTFILE%*
{{- end }}
